	; MK90 STMP/PIMP unmounter (BOOTL zone) for Basic [1.0/2.0]
	; Bs0Dd [bs0dd.net], 2025

	.ENABL AMA 	; Absolute addressing
	.ASECT		; Absolute section
	.= 0		; Beginning of cartridge
	NOP 		; Obligatory!
	
	LCDINA = 164000
	LCDINB = 164002
	SMPCMD = 164026
	SMPBAUD = 164022
	SMPCS = 164024
	SMPIO = 164020
	
	; ----------------------------
	;    M  a  i  n
	; ----------------------------
smpset:	
	bic #376, r5
	beq smz
	clr r5
	br saddi
smz:
	inc r5
	incb @#smnum
saddi:
	add #370, r5
	mov r5, @#REGI
	
start:
	call @#INITRAM	; OS initialisation
	call @#INITTTY
	inc @#DISCURS ; disable cursor
	inc @#DISIND ; disable indication
	br stcon

stcon:

	; LCD controller initialisation
	mov	@#SCRPTR,@#LCDINA
	mov	#104306,@#LCDINB
	
	; Print the Title
	bis #41, @#PRINTATTRS	; black background
	
	clr @#CURSX ; X=0
	clr @#CURSY ; Y=0

	mov #cartTitle, r1
	call @#PRINT

	bic #41, @#PRINTATTRS
	
	mov #select, r1
	call @#PRINT
	
KYFUC:
	clr @#SCANC
	mov	#342,@#SMPCS
	wait
	movb @#SCANC, r2
	cmpb r2, #333
	bne isvk
	mov #173000, @#exit+2
	br umount
isvk:
	cmpb r2, #373
	beq umount
	jmp @#173000 ; Reboot!
umount:
	mov #AUTORUN, r3
; Set the emulated file name - zero terminated in r3
	mov	#11,@#SMPBAUD ; CONST: transfer rate
	mov	#SMPCS,r1	 ; CONST: control/status register
	mov	#WAITCOMM,r2	 ; CONST: procedure 'wait for the transfer completed'
	bis	#10,@#REGI		; writing to the SMP
	mov	@#REGI,(r1)
	mov	#361,@#SMPCMD	;cmd: 0xF1 Select File Name
	jsr	pc,(r2)
	; Write everything at r3 until 0x00 (inclusing 0x00)
selloop:
	movb (r3), @#SMPIO
	jsr	pc,(r2)
	tstb (r3)
	beq smpdone
	inc r3
	br selloop
smpdone:
	tst	@#SMPCMD
	jsr	pc, (r2)
	mov	@#REGI, r5
	mov #177777, r0
	br waiting
	
; --------------------
.= 310
KEYB:
	.word KBVECT, 0
; --------------------

waiting:
	sob r0, waiting
exit:
	jmp @#SMPBOOT

cartTitle:
	.asciz \   Gen-UMOUNT 1.0   \
select:
	.ascii <16><12>\rAZMONTIROWATX OBRAZW \<17>\STMP/PIMP? \<16>\(smp\
smnum:
	.ascii \0\
	.asciz \)\<12><12><15>\zb - wYPOLNITX\<12><15>\wk - I ZAGRUZITXSQ,\<12><15>\     INA^E WOZWRAT.\
AUTORUN:
	.asciz \AUTORUN.BIN\
REGI:
	.word 0
	
; Fill free space (for glueing with EMPTYx0.BIN)
.=776
	.word 0

	;------------------------------------
	; OS Procedures and Vars (BASIC 1.0)
	;------------------------------------

	PRINTATTRS = 34072
	PRINT = 153414
	INITRAM = 153700
	INITTTY = 120536
	WAITCOMM = 174506
	KBVECT = 130514
	DISIND = 34074
	DISCURS = 34076
	CURSX = 34044
	CURSY = 33726
	SCANC = 34144
	SMPBOOT = 174216
	SCRPTR = 34022
